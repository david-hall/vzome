

group = 'com.vzome'
version = '7.0'

wrapper() {
   // Note that gradle version 6.0.1 can't find java3d in the ivy repository.
   // I think it's because http://jogamp.org/deployment/java3d/1.6.0-final/ivy.xml doesn't exist
   // Gradle 5.6.4 can find it without needing ivy.xml, 
   // so don't upgrade to 6.* yet, but maybe after java3d is replaced.
   gradleVersion = '5.6.4' //version required
}

// These environment variables are automatically available during a Jenkins build
ext.buildNumber = System.getenv( "BUILD_NUMBER" ) ?: 'dev'
ext.gitCommit = System.getenv( "GIT_COMMIT" ) ?: 'HEAD'
ext.githubRepoName = System.getenv( "GITHUB_REPO_NAME" ) ?: ''

// These environment variables must be configured in the Jenkins job explicitly.
ext.githubClientId = System.getenv( "GITHUB_CLIENT_ID" )
ext.githubClientSecret = System.getenv( "GITHUB_CLIENT_SECRET" )

ext.appName = 'vZome'
ext.edition = appName

// Specify common settings for all subprojects
subprojects {

  apply plugin: 'java'
  apply plugin: 'maven'
  apply plugin: 'eclipse'

  compileJava {
	// set these to 11 for Java 11
	// set them to 1.8 for Java 8
    sourceCompatibility = 11
    targetCompatibility = 11
    options.encoding = 'UTF-8'
  }

  repositories {
    mavenLocal()
    mavenCentral()
    
    // This is for the JSweet compiler
    maven { url "http://repository.jsweet.org/artifactory/libs-release-local" }
    maven { url "http://repository.jsweet.org/artifactory/libs-snapshot-local" }

    // this one is for javax.media.jai
    maven { url 'http://repository.springsource.com/maven/bundles/external' }

    // For EGit
    maven { url 'https://repo.eclipse.org/content/groups/releases' }

    ivy { // not really an Ivy repo, but this pattern lets us automate the bare JAR downloads for JOGL
      url "http://jogamp.org/deployment"
      layout "pattern", {
        artifact "[revision]/jar/[artifact].[ext]"
      }
    }
    ivy { // not really an Ivy repo, but this pattern lets us automate the bare JAR download for vecmath
      url "http://jogamp.org/deployment"
      patternLayout {
        artifact "[organization]/[revision]/[artifact].[ext]"
      }
    }
  }

  // see platform/mac/build.gradle, search for "error -5341"
  //    buildDir    = rootProject.buildDir
  group       = rootProject.group
  version     = rootProject.version
  description = rootProject.name + '-' + project.name
}

project(':desktop') {
    dependencies {
        compile project(':core')
    }
}

project(':server') {
    dependencies {
        compile project(':desktop')
    }
}
